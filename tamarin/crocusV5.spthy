
theory crocus_TF

begin

functions: adec/2, aenc/2, pk/1

equations: adec(aenc(message, secretkey), pk(secretkey)) = message


builtins: signing


// -------------------- Protocol Rules -------------------- //

rule Prover_1:   // choose the nonce for this run and send it
  [ Fr(~r) ]
--[ Start(~r) ]->
  [ Send( $Prover, pk(~r) )
  , Prover_State_1($Prover, ~r)
  ]

rule Verifier_1:   // receive the nonce, choose the commits and send them
  [ In( R ), Fr(~c1), Fr(~c2) ]
--[ Start(<~c1,~c2>) ]->
  [ Out( <~c1,~c2> )
  , Verifier_State_1( R, ~c1, ~c2 )
  ]

rule Prover_2:   // receive the commits, pre-compute replies, may leak, send ready
  [ Prover_State_1(P, r), In( <c1,c2> ), !Ltk(P, a) ]
--[  ]->
  [ Send(P, sign(pk(r), a) )
  , MayLeak(P, a)
  , Prover_State_2(P, r, c1, c2)
  ]

// receive ready, choose the actual challenge, send it --beginning fast phase
rule Verifier_2_1:   
  [ Verifier_State_1(R, c1, c2), In(sign), Fr(~b), !Pk(P, A) ]
--[ Send($Verifier, <~b, c1>)
  , Eq(verify(sign, R, A), true)
  ]->
  [ Out( <~b, c1> )
  , Verifier_State_2($Verifier, R, c1, c2, ~b, c1)
  ]

rule Verifier_2_2:   
  [ Verifier_State_1(R, c1, c2), In(sign), Fr(~b), !Pk(P, A) ]
--[ Send($Verifier, <~b, c2>)
  , Eq(verify(sign, R, A), true)
  ]->
  [ Out( <~b, c2> )
  , Verifier_State_2($Verifier, R, c1, c2, ~b, c2)
  ]

rule Prover_3:   // receive actual challenge, send corresponding reply
  let chal = <b, c>
      sign = sign(chal, a)
      response = aenc( <chal, sign>, r )
  in
  [ Prover_State_2(P, r, c1, c2), In( chal ), !Ltk(P, a) ]
--[ End(r) ]->
  [ Send( P, response ) ]

rule Verifier_3:   // receive reply, check and validate, end run --end fast phase
  let cleartext = adec(response, R)
      chal = fst(cleartext)
      sign = snd(cleartext)
  in
  [ Verifier_State_2(V, R, c1, c2, b, c), Recv(V, response), !Pk(P, A) ]
--[ Eq(chal, <b, c>), Eq(verify(sign, chal, A), true)
  , DBSec(V, P, chal, response)
  , End( <c1, c2> )
  ]->
  []




//GENERIC CODE AFTER THIS LINE
///////////////////////////////////////////////////////////////////////

// ------------------- Key Generation and Compromise --------------- //
rule LtkGen:
	[ Fr(~k) ]--[ Once($X) ]->[	!Ltk($X, ~k), !Pk($X, pk(~k)), Out(pk(~k)) ]

rule ShkGen:
	[ Fr(~k) ]--[ Once(<$X, $Y>) ]->[ !Shk($X, $Y, ~k) ]

rule LtkReveal:
	[ !Ltk($X, k) ]--[ !Compromise($X) ]->[ Out(k), !Compromise($X) ]

rule ShkRevealX:
	[ !Shk($X, $Y, k) ]--[ !Compromise($X) ]->[ Out(k), !Compromise($X) ]

rule ShkRevealY:
	[ !Shk($X, $Y, k) ]--[ !Compromise($Y) ]->[ Out(k), !Compromise($Y) ]

// ------------------- Network and Adversary  ----------------------- //
rule DBInject:
	[ In(m), !Compromise($X) ]-->[ Send($X, m) ]

rule DBSend:
	[ Send($X, m) ]--[ Send($X, m), Action($X) ]->[ Net(m), Out(m) ]

rule DBRecv:
	[ Net(m) ]--[ Action($Y), Recv($Y, m) ]->[ Recv($Y, m) ]

// -------------------- Collusion (Leakage) Rule -------------------- //
rule Leak:
	[ MayLeak($X, m) ]--[ Action($X), Collusion() ]->[ Out(m) ]

// ------------------- Reachability & Sanity Check ------------------ //
// Clean trace exists
lemma reachability:
	exists-trace
	"
	Ex V P chal resp #i /*#i2*/ #j #k #l.
		Send(V, chal)@i &
		//Recv(P, chal)@i2 &
		Send(P, resp)@j &
		Recv(V, resp)@k &
		DBSec(V, P, chal, resp)@l &
		i<j & j<k & k<l &
		not (Ex C #r. !Compromise(C)@r) &
		not (Ex #c. Collusion()@c)
	"

// Each run id is unique
lemma unique_runid:
	"All x #i #j. (Start(x)@i & Start(x)@j) ==> #i=#j"

// Each run ends after it starts
lemma end_after_start:
	"All x #j. End(x)@j ==> Ex #i. Start(x)@i & (#i=#j|i<j)"

// For each DBSec claim there was a fast phase before it
// *	Should this lemma fail, then the results for 
//		the security lemmas might be wrong
lemma fast_before_claim:
	"
	All V P chal resp #l. DBSec(V, P, chal, resp)@l ==>
		(Ex #i #j. Send(V, chal)@i & Recv(V, resp)@j & i<l & j<l)	|
		(Ex #j. !Compromise(V)@j)		
	"
// ------------------------ Restrictions ---------------------------- //
// Only traces where Once(x) happened once
restriction once:
	"All x #i #j. (Once(x)@i & Once(x)@j) ==> #i=#j"

restriction equals:
    "
    All a b #t1. Eq(a,b)@t1 ==>
    (
        a = b
    )
    "

/*
// Runs can always be completed
restriction completion:
	"
	All run #i. Start(run)@i ==> Ex #j. End(run)@j
	"
*/
// ----------------- Expiry Rule and Restrictions ------------------- //
rule SetExpiry:
	[ ]--[ Expiry(), Once('expiry') ]->[ ]

restriction no_collusion_after_expiry:
	"All #i #j. (Expiry()@i & Collusion()@j) ==> j<i"

restriction sessions_complete_before_expiry:
	"All x #i #e. (Start(x)@i & Expiry()@e & i<e) ==> Ex #j. (End(x)@j & j<e)"


////////////////////////////////////////////////////////////////////////
// ------------------------ Security Lemmas ------------------------- //
////////////////////////////////////////////////////////////////////////

// For each trace (in which collusion did not occurr) it is the case that
// * 	EITHER the verifier is compromised,
// * 	OR the prover is not and he was active during the fast phase,
// * 	OR the prover is compromised and there was a compromised 
//		prover active during the fast phase.
lemma dbsec:
	"
	All V P chal resp #l. 
		DBSec(V, P, chal, resp)@l ==>
			(	//EITHER P is active in fast phase
				Ex #i #j #k.
					Send(V, chal)@i &
					Action(P)@j &
					Recv(V, resp)@k &
					i<j & j<k
			) | 
			(	//OR (P is compromised AND there's a compromised 
				//agent active in fast phase)
				Ex P2 #p #p2 #i #j #k.
					!Compromise(P)@p &
					!Compromise(P2)@p2 &
					Send(V, chal)@i &
					Action(P2)@j &
					Recv(V, resp)@k &
					i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. Collusion()@j) //this is just to drop traces w. collusion
	"

// dbsec provided that neither the prover nor verifier are compromised
// * 	If this lemma fails, then there's a mafia fraud
// * 	If this lemma holds and dbsec fails, then there's a 
//		distance fraud, or a distance hijacking, or both
lemma dbsec_hnst:
	"
	All V P chal resp #l. 
		DBSec(V, P, chal, resp)@l ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. !Compromise(P)@j) |
			(Ex #j. Collusion()@j) //this is just to drop traces w. collusion
	"

// dbsec_hnst with collusion
lemma dbsec_hnst_collusion:
	"
	All V P chal resp #l.
		DBSec(V, P, chal, resp)@l ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. !Compromise(P)@j)
	"

// post-collusion security w.r.t dbsec_hnst
// *	If this lemma holds and dbsec_hnst_collusion fails,
//		then there's a terrorist fraud
lemma dbsec_hnst_star:
	"
	All V P chal resp #e #l.
		(DBSec(V, P, chal, resp)@l & Expiry()@e & e<l) ==>
			(Ex #i #j #k.
				Send(V, chal)@i &
				Action(P)@j &
				Recv(V, resp)@k &
				i<j & j<k
			) |
			(Ex #j. !Compromise(V)@j) |
			(Ex #j. !Compromise(P)@j)
	"

end


